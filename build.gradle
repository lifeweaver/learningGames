import com.badlogic.gdx.tools.texturepacker.TexturePacker

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        jcenter()
    }
    dependencies {
        classpath "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: 'groovy'

    version = '1.0'
    ext {
        appName = "myGames"
        gdxVersion = gdxVersion
        roboVMVersion = '2.3.3'
        box2DLightsVersion = '1.4'
        ashleyVersion = '1.7.4'
        aiVersion = '1.8.2'
        groovyVersion = '3.0.6'
        logbackVersion = '1.2.3'
        texturePacker = ["core/assets/input/", "core/assets/", "atlas"]
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
    }

    dependencies {
        implementation "org.codehaus.groovy:groovy-all:${groovyVersion}"
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
        implementation group: 'ch.qos.logback', name: 'logback-core', version: "$logbackVersion"
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: "$logbackVersion"
        implementation 'com.github.Anuken:GdxGifRecorder:1.4'
    }
}

project(":desktop") {
    apply plugin: 'java'
    apply plugin: 'groovy'

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.ashley:ashley:${ashleyVersion}"
        compile "com.badlogicgames.gdx:gdx-ai:${aiVersion}"
    }
}

project(":core") {
    apply plugin: 'java'
    apply plugin: 'groovy'

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        compile "com.badlogicgames.ashley:ashley:${ashleyVersion}"
        compile "com.badlogicgames.gdx:gdx-ai:${aiVersion}"
        compile "com.codeandweb.physicseditor:gdx-pe-loader:1.1.0"
    }
}

tasks.eclipse.doLast {
    delete ".project"
}

task texturePacker {
    doLast {
        if(project.ext.has('texturePacker')) {
            logger.info "Calling texturePacker: ${texturePacker}"
            TexturePacker.Settings settings = new TexturePacker.Settings(combineSubdirectories: true, flattenPaths: false)
            TexturePacker.process(settings, texturePacker[0], texturePacker[1], texturePacker[2])
        }
    }
}